/*
Interface to output attack traces from MulVAL reasoning engine
Author(s) : Xinming Ou
Copyright (C) 2011, Argus Cybersecurity Lab, Kansas State University

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/
:- import length/2 from lists.
:- table mvTrc/1.

/* This function will be called by the instrumented rules to record derivation mvTrcs */
assert_trace(TraceStep) :-
	write_clause_to_stdout(possible_duplicate_trace_step(TraceStep)).


/* attack simulation will use the uninstrumented rules */
attack_simulation(Goal) :-
	attackGoal(Goal),
	call(Goal).

/* attack simulation with mvTrc will use the instrumented rules */
attack_simulation_trace(OutputFile) :-
	findall(Goal, attack_simulation(Goal), L),
	abolish_table_pred(mvTrc/1),
	tell(OutputFile),
	iterate(primitive(C),
	        (C =.. [P|Args],
		 length(Args, Arity),
	         write_canonical(primitive(P, Arity)), writeln('.'))),
	iterate(derived(C),
	        (C =.. [P|Args],
		 length(Args, Arity),
	         write_canonical(derived(P, Arity)), writeln('.'))),
	iterate(meta(C),
	        (C =.. [P|Args],
		 length(Args, Arity),
	         write_canonical(meta(P, Arity)), writeln('.'))),
	nl,
	list_apply(L, output_trace),
	told.

output_trace(Goal) :-
	write_clause_to_stdout(attack(Goal)),
	((traceMode(completeTrace);traceMode(completeTrace2))  ->
		addArgumentEnd(Goal, _HeadTraceNumber, NewGoal);
	  traceMode(trace) ->
	  	NewGoal = Goal;
	  writeln('#######################Error: Unsupported trace mode or no trace mode defined.'), !, fail),	
	mvTrc(NewGoal).

